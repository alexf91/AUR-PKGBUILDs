# Maintainer: Alexander Fasching <fasching.a91@gmail.com>
# Contributor: Andreas Schreiner <andreas.schreiner@sonnenmulde.at>

pkgname=freedv-gui
pkgver="1.4"
pkgrel=1
pkgdesc="Digital Voice for Radio Amateurs"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://freedv.org/"
depends=('portaudio' 'hamlib' 'sox' 'wxgtk' 'codec2' 'speex' 'libao')
makedepends=('cmake')
source=("https://github.com/drowe67/freedv-gui/archive/v$pkgver.tar.gz"
        "optional-lpcnetfreedv.patch")
sha512sums=('f33c206497761914eb09b67983771da18bc5be70d1a333ae2d57b9d0f9c579f567a75aea4b67068dd7fd1856b962b37cc6eb4dc895acaab4d70e22718443338f'
            'bf0055f6251950495cb07d54e2ad7783c62bd87f3654e5ee52e08af19d78c38f9ea51d103b2d1146ec13f67f368369cd3f99218113075b020f5935a6a7128504')


build() {
  cd "$pkgname-$pkgver"
  patch -p1 < ../optional-lpcnetfreedv.patch
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX="/usr" -DCMAKE_BUILD_TYPE=Release ..
  make
}

package() {
  cd "$pkgname-$pkgver"
  install -m644 -D COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -m644 -D README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  cd "build"
  make DESTDIR="$pkgdir" install
}
