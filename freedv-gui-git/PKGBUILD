# Maintainer: Alexander Fasching <fasching.a91@gmail.com>
# Contributor: Andreas Schreiner <andreas.schreiner@sonnenmulde.at>

pkgname=freedv-gui-git
pkgver=v1.6.1.r0.g5e9f5c3
pkgrel=1
pkgdesc="Digital Voice for Radio Amateurs"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://freedv.org/"
depends=('portaudio' 'hamlib' 'sox' 'wxgtk3' 'codec2' 'speex' 'libao')
makedepends=('cmake')
source=("git+https://github.com/drowe67/freedv-gui.git"
        "git+https://github.com/drowe67/LPCNet.git"
        "0001-Use-local-lpcnet-build-dir.patch")
sha512sums=('SKIP'
            'SKIP'
            'a293da43032953deb906ce16d7da29fb5e5a67c9f9040fd494b3d4f28d085b2224841f0ee6b03fe32ca2c923ebcdc87cdd3938262683e748a4968f7191d3b8e2')

pkgver() {
  cd "freedv-gui"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  # Build LPCNet
  cd "$srcdir/LPCNet"
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX="/usr" -DCMAKE_BUILD_TYPE=Release ..
  make

  # Build freedv-gui
  cd "$srcdir/freedv-gui"
  patch -p1 < ../0001-Use-local-lpcnet-build-dir.patch
  mkdir -p build
  cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DLPCNET_BUILD_DIR="$srcdir/LPCNet/build" \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
}

package() {
  # Install LPCNet
  cd "$srcdir/LPCNet/build"
  make DESTDIR="$pkgdir" install

  cd "$srcdir/freedv-gui"
  install -m644 -D COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -m644 -D README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  cd "build"
  make DESTDIR="$pkgdir" install
}
